plugins {
    id 'io.quarkus' version "$quarkusVersion"
}

dependencies {
    implementation platform("io.quarkus:quarkus-bom:$quarkusVersion")

    implementation 'io.quarkus:quarkus-jsonb'
    implementation 'io.quarkus:quarkus-resteasy'
    implementation 'io.quarkus:quarkus-security'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-jwt'
    implementation 'io.quarkus:quarkus-smallrye-metrics'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-smallrye-reactive-messaging'

    implementation "com.github.jsonld-java:jsonld-java:$jsonldVersion"
    implementation "com.github.spullara.mustache.java:compiler:$mustacheVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "commons-codec:commons-codec:$commonsCodecVersion"
    implementation "org.apache.jena:jena-arq:$jenaVersion"
    implementation "org.apache.jena:jena-rdfconnection:$jenaVersion"
    implementation "org.apache.jena:jena-tdb2:$jenaVersion"

    implementation project(":trellis-api")
    implementation project(":trellis-app")
    implementation project(":trellis-audit")
    implementation project(":trellis-cache")
    implementation project(":trellis-cdi")
    implementation project(":trellis-constraint")
    implementation project(":trellis-file")
    implementation project(":trellis-http")
    implementation project(":trellis-jena")
    implementation project(":trellis-jwt")
    implementation project(":trellis-namespace")
    implementation project(":trellis-notification-jsonb")
    implementation project(":trellis-rdfa")
    implementation project(":trellis-reactive")
    implementation project(":trellis-vocabulary")
    implementation project(":trellis-webac")

    // Persistence store
    if (project.hasProperty("triplestore")) {
        implementation project(":trellis-triplestore")
    } else {
        implementation 'io.quarkus:quarkus-agroal'
        implementation 'io.quarkus:quarkus-flyway'
        implementation 'io.quarkus:quarkus-jdbc-postgresql'
        implementation project(":trellis-jdbc")

        runtimeOnly "org.yaml:snakeyaml:$snakeyamlVersion"
    }

    runtimeOnly "jakarta.activation:jakarta.activation-api:$activationApiVersion"
    runtimeOnly "jakarta.xml.bind:jakarta.xml.bind-api:$jaxbApiVersion"
    runtimeOnly "org.postgresql:postgresql:$postgresVersion"

    testImplementation project(":trellis-test")
    testImplementation "io.quarkus:quarkus-junit5"
    testImplementation "io.rest-assured:rest-assured"
    testImplementation "org.jboss.resteasy:resteasy-client"
}

test {
    systemProperty 'com.arjuna.ats.arjuna.objectstore.objectStoreDir', "$buildDir/data/ObjectStore"
    systemProperty 'trellis.file.binary-path', "$buildDir/data/binaries"
    systemProperty 'trellis.file.memento-path', "$buildDir/data/mementos"
    systemProperty 'trellis.namespace.mapping', 'dc11=http://purl.org/dc/elements/1.1/,,foo= , =bar,baz, = '
    systemProperty 'mp.jwt.verify.publickey.location', 'https://www.trellisldp.org/tests/jwks.json'

    if (project.hasProperty("triplestore")) {
        systemProperty 'trellis.triplestore.rdf-location', "$buildDir/data/rdf"
    } else {
        systemProperty 'com.arjuna.ats.arjuna.objectstore.objectStoreDir', "$buildDir/data/ObjectStore"
        systemProperty 'quarkus.datasource.jdbc.url', 'jdbc:postgresql://localhost/postgres'
        systemProperty 'quarkus.flyway.migrate-at-start', 'true'
    }
}

sonarqube {
    skipProject = true
}

buildNative.enabled = false

