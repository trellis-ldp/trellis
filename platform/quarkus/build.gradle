plugins {
    id 'io.quarkus'
}

repositories {
    mavenLocal() {
        content {
            includeGroup "org.trellisldp"
        }
    }
    mavenCentral()
    // This is required for snyk integration with the quarkus project
    // Once quarkus has better support for multi-project builds, this can be removed
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

def installForQuarkus = [
    "trellis-api",
    "trellis-app",
    "trellis-audit",
    "trellis-auth-jwt",
    "trellis-cache",
    "trellis-cdi",
    "trellis-constraint-rules",
    "trellis-event-jsonb",
    "trellis-file",
    "trellis-http",
    "trellis-io-jena",
    "trellis-namespaces",
    "trellis-rdfa",
    "trellis-reactive",
    "trellis-triplestore",
    "trellis-vocabulary",
    "trellis-webac"
]

dependencies {
    implementation enforcedPlatform("io.quarkus:quarkus-bom:$quarkusVersion")

    implementation "org.trellisldp:trellis-api:${project.version}"
    implementation "org.trellisldp:trellis-app:${project.version}"
    implementation "org.trellisldp:trellis-audit:${project.version}"
    implementation "org.trellisldp:trellis-auth-jwt:${project.version}"
    implementation "org.trellisldp:trellis-cache:${project.version}"
    implementation "org.trellisldp:trellis-cdi:${project.version}"
    implementation "org.trellisldp:trellis-constraint-rules:${project.version}"
    implementation "org.trellisldp:trellis-event-jsonb:${project.version}"
    implementation "org.trellisldp:trellis-file:${project.version}"
    implementation "org.trellisldp:trellis-http:${project.version}"
    implementation "org.trellisldp:trellis-io-jena:${project.version}"
    implementation "org.trellisldp:trellis-namespaces:${project.version}"
    implementation "org.trellisldp:trellis-rdfa:${project.version}"
    implementation "org.trellisldp:trellis-reactive:${project.version}"
    implementation "org.trellisldp:trellis-triplestore:${project.version}"
    implementation "org.trellisldp:trellis-vocabulary:${project.version}"
    implementation "org.trellisldp:trellis-webac:${project.version}"

    implementation 'io.quarkus:quarkus-jsonb'
    implementation 'io.quarkus:quarkus-resteasy'
    implementation 'io.quarkus:quarkus-security'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-jwt'
    implementation 'io.quarkus:quarkus-smallrye-metrics'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-smallrye-reactive-messaging'

    implementation "com.github.spullara.mustache.java:compiler:$mustacheVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "org.apache.jena:jena-arq:$jenaVersion"
    implementation "org.apache.jena:jena-rdfconnection:$jenaVersion"
    implementation "org.apache.jena:jena-tdb2:$jenaVersion"

    runtimeOnly "jakarta.activation:jakarta.activation-api:$activationApiVersion"
    runtimeOnly "jakarta.xml.bind:jakarta.xml.bind-api:$jaxbApiVersion"

    testImplementation project(":trellis-test")
    testImplementation "io.quarkus:quarkus-junit5"
    testImplementation "io.rest-assured:rest-assured"
    testImplementation "org.jboss.resteasy:resteasy-client"
}

test {
    systemProperty 'com.arjuna.ats.arjuna.objectstore.objectStoreDir', "$buildDir/data/ObjectStore"
    systemProperty 'trellis.file.binary-path', "$buildDir/data/binaries"
    systemProperty 'trellis.file.memento-path', "$buildDir/data/mementos"
    systemProperty 'trellis.triplestore.rdf-location', "$buildDir/data/rdf"
    systemProperty 'trellis.namespaces.mapping', 'dc11=http://purl.org/dc/elements/1.1/,,foo= , =bar,baz, = '
    systemProperty 'mp.jwt.verify.publickey.location', 'https://www.trellisldp.org/tests/jwks.json'
    systemProperty 'trellis.http.weak-etag', 'true'
}

sonarqube {
    skipProject = true
}

rootProject.childProjects.each { n, p ->
    if (installForQuarkus.contains(p.name)) {
        tasks.compileJava.dependsOn p.tasks.install
    }
}

buildNative.enabled = false

java {
    // because this isn't built as a JPMS module
    if (project.hasProperty("jpms") && JavaVersion.current().isJava11Compatible()) {
        disableAutoTargetJvm()
    }
}

// This subproject is not part of the release and its dependencies should not
// be considered during the release process.
release {
    ignoredSnapshotDependencies = installForQuarkus.collect { "org.trellisldp:" + it }
}

