apply plugin: 'java-library'
apply plugin: 'biz.aQute.bnd.builder'

description = 'Trellis HTTP API'

ext {
    moduleName = 'org.trellisldp.http'
    testModules = ['org.trellisldp.io', 'org.trellisldp.audit', 'org.trellisldp.agent', 'org.trellisldp.constraint', 'smallrye.config']
}

dependencies {
    api("javax.annotation:javax.annotation-api:$javaxAnnotationsVersion")
    api("javax.enterprise:cdi-api:${cdiApiVersion}")
    api("javax.inject:javax.inject:$javaxInjectVersion")
    api("javax.ws.rs:javax.ws.rs-api:$jaxrsVersion")
    api("org.apache.commons:commons-rdf-api:$commonsRdfVersion")
    api("org.eclipse.microprofile.metrics:microprofile-metrics-api:$microprofileMetricsVersion")
    api project(':trellis-api')

    implementation("commons-codec:commons-codec:$commonsCodecVersion")
    implementation("commons-io:commons-io:$commonsIoVersion")
    implementation("javax.xml.bind:jaxb-api:$jaxbVersion")
    implementation("org.apache.commons:commons-lang3:$commonsLangVersion")
    implementation("org.eclipse.microprofile.config:microprofile-config-api:$microprofileConfigVersion")
    implementation("org.slf4j:slf4j-api:$slf4jVersion")
    implementation project(':trellis-vocabulary')

    testImplementation("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
    testImplementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    testImplementation("org.apache.commons:commons-rdf-simple:$commonsRdfVersion")
    testImplementation("io.smallrye:smallrye-config:$smallryeConfigVersion")
    testImplementation("org.glassfish.jersey.core:jersey-server:$jerseyVersion")
    testImplementation("org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:$jerseyVersion")
    testImplementation("org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion")
    testImplementation("org.glassfish.jersey.connectors:jersey-apache-connector:$jerseyVersion")
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
    testImplementation project(':trellis-agent')
    testImplementation project(':trellis-audit')
    testImplementation project(':trellis-constraint-rules')
    testImplementation project(':trellis-event-serialization')
    testImplementation project(':trellis-io-jena')

    testRuntimeClasspath("javax.activation:javax.activation-api:$activationApiVersion")
    testRuntimeClasspath("ch.qos.logback:logback-classic:$logbackVersion")
}

if (project.sourceCompatibility.isJava11Compatible()) {
    test {
        inputs.property("moduleName", moduleName)
        doFirst {
            jvmArgs += [
                '--add-opens', "$moduleName/org.trellisldp.http.impl=org.junit.platform.commons",
                '--add-opens', "$moduleName/org.trellisldp.http.impl=org.mockito",
                '--add-opens', "$moduleName/org.trellisldp.http.core=org.junit.platform.commons",
                '--add-opens', "$moduleName/org.trellisldp.http.core=org.mockito",
                '--add-opens', "java.base/java.net=jersey.client",
            ]
        }
    }
}
