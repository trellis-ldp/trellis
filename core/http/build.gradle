plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder'
}

description = 'Trellis HTTP'

ext {
    moduleName = 'org.trellisldp.http'
    testModules = ['org.trellisldp.jena', 'org.trellisldp.audit', 'org.trellisldp.constraint', 'smallrye.config',
        'com.fasterxml.jackson.databind', 'org.apache.jena.commonsrdf', 'com.fasterxml.jackson.core',
        'org.apache.commons.io']
}

dependencies {
    api platform("org.eclipse.microprofile:microprofile:$microprofileVersion")

    api "jakarta.inject:jakarta.inject-api:$injectApiVersion"
    api "jakarta.enterprise:jakarta.enterprise.cdi-api:$cdiApiVersion"
    api "jakarta.annotation:jakarta.annotation-api:$annotationApiVersion"
    api "jakarta.ws.rs:jakarta.ws.rs-api:$jaxrsApiVersion"
    api "org.apache.commons:commons-rdf-api:$commonsRdfVersion"
    api "org.eclipse.microprofile.metrics:microprofile-metrics-api"
    api "org.eclipse.microprofile.openapi:microprofile-openapi-api"
    api project(':trellis-api')
    api project(':trellis-common')

    implementation "commons-codec:commons-codec:$commonsCodecVersion"
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:$jaxbApiVersion"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    implementation "org.eclipse.microprofile.config:microprofile-config-api"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation project(':trellis-vocabulary')

    testImplementation platform("org.glassfish.jersey:jersey-bom:$jerseyVersion")
    testImplementation platform("com.fasterxml.jackson:jackson-bom:$jacksonVersion")

    testImplementation "com.fasterxml.jackson.core:jackson-core"
    testImplementation "com.fasterxml.jackson.core:jackson-databind"
    testImplementation "commons-io:commons-io:$commonsIoVersion"
    testImplementation "io.smallrye.config:smallrye-config:$smallryeConfigVersion"
    testImplementation "org.apache.jena:jena-commonsrdf:$jenaVersion"
    testImplementation "org.glassfish.jersey.core:jersey-server"
    testImplementation "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2"
    testImplementation "org.glassfish.jersey.inject:jersey-hk2"
    testImplementation "org.glassfish.jersey.connectors:jersey-apache-connector"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation project(':trellis-audit')
    testImplementation project(':trellis-constraint')
    testImplementation project(':trellis-jena')
    testImplementation project(':trellis-notification-jackson')

    testRuntimeOnly "jakarta.activation:jakarta.activation-api:$activationApiVersion"
    testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs += [
            '--add-opens', "$moduleName/$moduleName=hk2.utils",
            '--add-opens', "$moduleName/org.trellisldp.http.impl=org.junit.platform.commons",
            '--add-opens', "$moduleName/org.trellisldp.http.impl=org.mockito",
            '--add-opens', "java.base/java.net=jersey.client",
        ]
    }
}
