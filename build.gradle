plugins {
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'org.sonarqube' version '2.6.1'
}

allprojects {

    apply plugin: 'maven'
    apply plugin: 'jacoco'

    ext {
        jacocoVersion = '0.8.0'
    }

    jacoco {
        toolVersion = jacocoVersion
    }

    group = 'org.trellisldp'

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    release {
        tagTemplate = '$name-$version'
        git {
            signTag = true
        }
    }

}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.hierynomus.license'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        vendor = 'Trellis LDP'
        homepage = 'https://www.trellisldp.org'
        docURL = 'https://trellis-ldp.github.io/trellis/apidocs/'
        license = 'Apache 2'

        /* Dependencies */
        activationApiVersion = '1.2.0'
        activeMqVersion = '5.15.3'
        commonsCodecVersion = '1.11'
        commonsCollectionsVersion = '4.1'
        commonsIoVersion = '2.6'
        commonsLangVersion = '3.7'
        commonsRdfVersion = '0.5.0'
        dropwizardVersion = '1.2.3'
        javaxAnnotationsVersion = '1.3.1'
        javaxInjectVersion = '1_2'
        javaxJsonVersion = '1.1.2'
        jaxrsVersion = '2.1'
        jenaVersion = '3.6.0'
        jmsApiVersion = '2.0.1'
        kafkaVersion = '1.0.0_1'
        metricsVersion = '4.0.2'
        mustacheVersion = '0.9.5_1'
        rabbitMqVersion = '5.1.2'
        slf4jVersion = '1.7.25'
        jwtVersion = '0.9.0'
        jaxbVersion = '2.3.0'

        /* Transitive dependencies (for Karaf provisioning) */
        guavaVersion = '20.0' // via mustache 0.9.5

        /* Testing */
        apiguardianVersion = '1.0.0'
        awaitilityVersion = '3.0.0'
        commonsTextVersion = '1.2'
        jacksonVersion = '2.9.4'
        jerseyVersion = '2.26'
        jsonVersion = '1.1.2'
        junitVersion = '5.0.2'
        junitPlatformVersion = '1.0.2'
        logbackVersion = '1.2.3'
        mockitoVersion = '2.13.0'
        xercesVersion = '2.11.0'

        /* OSGi */
        spiflyVersion = '1.0.8'
        projectOsgiVersion = project.version.replaceAll("-SNAPSHOT", ".SNAPSHOT")
        jenaVersionRange = '[3.5,4)'
    }

    jar {
        from ("$rootDir/LICENSE") {
            into "META-INF"
        }
    }

    test {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
        if (org.gradle.internal.jvm.Jvm.current().getJavaVersion().isJava9Compatible()) {
            jvmArgs '--add-modules', 'java.xml.bind'
        }
    }

    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = '8.1'
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from ("$rootDir/LICENSE") {
            into "META-INF"
        }
        from javadoc
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from ("$rootDir/LICENSE") {
            into "META-INF"
        }
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar
        archives sourceJar
    }

    license {
        include "**/*.java"
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    processResources {
        outputs.upToDateWhen { false }
        filesMatching('**/features.xml') {
            expand project.properties
        }
    }

    signing {
        required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories.mavenDeployer {
            def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : ""
            def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ""
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                packaging 'jar'
                url 'https://www.trellisldp.org'
                inceptionYear '2017'
                name 'Trellis Linked Data Server'
                description 'The core components for a Trellis linked data server'

                organization {
                    name project.vendor
                    url project.homepage
                }

                developers {
                    developer {
                        id 'acoburn'
                        name 'Aaron Coburn'
                        email 'acoburn (at) apache (dot) org'
                        organization = 'Trellis LDP'
                        organizationUrl 'https://www.trellisldp.org'
                        roles {
                            role 'developer'
                        }
                        timezone '-5'
                    }
                }

                scm {
                    connection 'scm:git:git://github.com/trellis-ldp/trellis.git'
                    developerConnection 'scm:git:git@github.com/trellis-ldp/trellis.git'
                    url 'https://github.com/trellis-ldp/trellis'
                    tag 'HEAD'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                        comments 'Copyright (c) 2017-2018 Trellis LDP'
                    }
                }
            }
        }
    }


    task docs(type: Javadoc) {
        outputs.upToDateWhen { false }
        source sourceSets.main.allJava
        classpath = files(sourceSets.main.compileClasspath)
        destinationDir = new File(projectDir, "docs/${version}")
        options {
            links "https://docs.oracle.com/javase/8/docs/api/"
            links 'https://docs.oracle.com/javaee/7/api/'
            links 'https://trellis-ldp.github.io/trellis/apidocs/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
        }
    }

    afterReleaseBuild.dependsOn docs
    afterReleaseBuild.dependsOn uploadArchives
}

apply plugin: 'java'

task apidocs(type: Javadoc, dependsOn: getTasksByName('docs', true)) {
    outputs.upToDateWhen { false }
    destinationDir = new File(projectDir, "docs/apidocs")
    title = "Trellis Linked Data Server Documentation"
    exclude '**/impl/*'
    options {
        memberLevel = JavadocMemberLevel.PUBLIC
        links "https://docs.oracle.com/javase/8/docs/api/"
        links 'https://docs.oracle.com/javaee/7/api/'
        links 'https://trellis-ldp.github.io/trellis/apidocs/'
        links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
    }

    source subprojects.collect {project -> project.sourceSets.main.allJava }
    classpath = files(subprojects.collect {project -> project.sourceSets.main.compileClasspath})
}

sonarqube {
    properties {
        property "sonar.projectName", "Trellis Linked Data Server"
        property "sonar.projectKey", "org.trellisldp:trellis"
        property "sonar.links.homepage", "https://www.trellisldp.org"
        property "sonar.links.issue", "https://github.com/trellis-ldp/trellis/issues"
        property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/trellis.git"
    }
}


task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.findAll { it.name != 'trellis-karaf' }.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    dependsOn 'jacocoRootReport'
}

