plugins {
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'com.github.spotbugs' version '2.0.1'
    id 'com.gradle.build-scan' version '3.0'
    id 'net.researchgate.release' version '2.8.1'
    id 'org.owasp.dependencycheck' version '5.2.4'
    id 'org.sonarqube' version '2.8'
    id 'biz.aQute.bnd.builder' version '4.1.0' apply false
    id 'com.palantir.docker' version '0.22.1' apply false
    id 'nebula.ospackage' version '8.0.3' apply false
}

ext {

    /* Core dependencies */
    commonsCodecVersion = '1.13'
    commonsIoVersion = '2.6'
    commonsLangVersion = '3.9'
    commonsRdfVersion = '0.5.0'
    slf4jVersion = '1.7.29'

    /* JavaEE */
    activationApiVersion = '1.2.1'
    annotationApiVersion = '1.3.5'
    cdiApiVersion = '2.0.2'
    injectApiVersion = '1.0'
    jaxbApiVersion = '2.3.2'
    jaxrsApiVersion = '2.1.6'
    jmsApiVersion = '2.0.3'
    jsonbApiVersion = '1.0.2'
    managementApiVersion = '1.1.4'
    validationApiVersion = '2.0.2'

    /* Microprofile */
    microprofileConfigVersion = '1.3'
    microprofileHealthVersion = '2.1'
    microprofileMetricsVersion = '2.2'
    microprofileOpenapiVersion = '1.1.2'
    microprofileReactiveMessagingVersion = '1.0'

    /* Component dependencies */
    activeMqVersion = '5.15.9'
    dropwizardVersion = '1.3.16'
    guavaVersion = '28.1-jre'
    jacksonVersion = '2.10.1'
    jenaVersion = '3.13.1'
    jjwtVersion = '0.10.7'
    jsonldVersion = '0.12.5'
    kafkaVersion = '2.3.1'
    mustacheVersion = '0.9.6'
    quarkusVersion = '0.28.1'
    rabbitMqVersion = '5.7.3'
    rxjavaVersion = '2.2.14'
    thriftVersion = '0.13.0'

    /* Testing */
    apiguardianVersion = '1.1.0'
    awaitilityVersion = '4.0.1'
    bouncycastleVersion = '1.64'
    commonsTextVersion = '1.8'
    cxfVersion = '3.3.3'
    glassfishJaxbVersion = '2.3.2'
    glassfishJsonVersion = '1.1.6'
    jerseyVersion = '2.29.1'
    junitVersion = '5.5.2'
    junitLauncherVersion = '1.5.2'
    hamcrestVersion = '2.2'
    logbackVersion = '1.2.3'
    mockitoVersion = '3.1.0'
    qpidVersion = '7.1.5'
    sleepycatVersion = '18.3.12'
    smallryeConfigVersion = '1.3.11'
    smallryeHealthVersion = '2.1.0'
    smallryeReactiveVersion = '1.0.8'
    smallryeReactiveOperatorsVersion = '1.0.10'
    weldVersion = '2.0.1.Final'
    yassonVersion = '1.0.5'

    /* OSGi */
    commonsCompressVersion = '1.19'
    glassfishInjectVersion = '2.5.0-b62'
    elVersion = '3.0.3'
    failureaccessVersion = '1.0.1'
    interceptorVersion = '1.2.5'
    jenaOsgiVersion = '3.13.1'
    jenaVersionRange = '[3.13,4)'
    kafkaOsgiVersion = '2.2.0_1'
    karafVersion = '4.2.7'
    mustacheOsgiVersion = '0.9.6_1'
    osgiVersion = '6.0.0'
    osgiCompendiumVersion = '5.0.0'
    paxExamVersion = '4.13.1'
    spiflyVersion = '1.2.3'
    // ActiveMQ requires Spring4, but Karaf 4.2 includes Spring5, so this forces an earlier spring dependency
    springFeatureVersion = '4.1.0'

    omitFromJacocoReporting = [
        'trellis-bom',
        'trellis-karaf',
        'trellis-openliberty',
        'trellis-osgi',
        'trellis-server',
        'trellis-test'
    ]

    omitFromMavenPublishing = [
        'trellis-osgi',
        'trellis-server'
    ]
}

allprojects { subproj ->

    apply plugin: 'checkstyle'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'pmd'
    apply plugin: 'signing'

    ext {
        vendor = 'Trellis LDP'
        homepage = 'https://www.trellisldp.org'
        docURL = 'https://www.trellisldp.org/docs/trellis/current/apidocs/'
        license = 'Apache 2'

        jacocoVersion = '0.8.5'
        checkstyleVersion = '8.26'
        pmdVersion = '6.19.0'
        spotbugsVersion = '3.1.12'
    }

    jacoco.toolVersion = jacocoVersion

    group = 'org.trellisldp'

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.apiguardian:apiguardian-api:${apiguardianVersion}"
        testRuntimeClasspath "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testRuntimeClasspath "org.junit.platform:junit-platform-launcher:${junitLauncherVersion}"
    }

    dependencyCheck {
        analyzers {
            assemblyEnabled = false
            retirejs {
                enabled = false
            }
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    release {
        tagTemplate = '$name-$version'
        git {
            /* Note: for maintenance branches, change this to the appropriate branch name. */
            requireBranch = 'master'
            signTag = true
        }
    }

    configurations.all {
        if (JavaVersion.current().isJava11Compatible() && subproj.hasProperty("jpms")) {
            exclude group: 'commons-logging', module: 'commons-logging'
            exclude group: 'com.google.code.findbugs', module: 'jsr305'
        }
        resolutionStrategy {
            force "org.apache.thrift:libthrift:$thriftVersion"
        }
        resolutionStrategy.dependencySubstitution {
            substitute module("org.apache.geronimo.specs:geronimo-annotation_1.2_spec") with module ("jakarta.annotation:jakarta.annotation-api:$annotationApiVersion")
            substitute module("org.apache.geronimo.specs:geronimo-jms_1.1_spec") with module("jakarta.jms:jakarta.jms-api:$jmsApiVersion")
            substitute module("org.apache.geronimo.specs:geronimo-j2ee-management_1.1_spec") with module("jakarta.management.j2ee:jakarta.management.j2ee-api:$managementApiVersion")
            substitute module("org.glassfish.hk2.external:jakarta.inject") with module("jakarta.inject:jakarta.inject-api:$injectApiVersion")
            substitute module("javax.enterprise:cdi-api") with module("jakarta.enterprise:jakarta.enterprise.cdi-api:$cdiApiVersion")
            substitute module("javax.annotation:javax.annotation-api") with module("jakarta.annotation:jakarta.annotation-api:$annotationApiVersion")
            substitute module("javax.inject:javax.inject") with module("jakarta.inject:jakarta.inject-api:$injectApiVersion")
        }
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group.startsWith('com.fasterxml.jackson.')) {
                details.useVersion jacksonVersion
                details.because 'Jackson modules should use a consistent version'
            }
        }
    }
}

subprojects { subproj ->

    def jpms = JavaVersion.current().isJava11Compatible() && subproj.hasProperty("jpms") &&
            sourceSets.main.allJava.getFiles().stream().map { file -> file.getName() }
                    .anyMatch { file -> file == "module-info.java"}

    sourceCompatibility = jpms ? 11 : 8
    targetCompatibility = jpms ? 11 : 8

    afterEvaluate {
        if (jpms) {
            compileJava {
                inputs.property("moduleName", moduleName)
                doFirst {
                    options.compilerArgs = [
                        '--module-path', classpath.asPath,
                    ]
                    classpath = files()
                }
            }
            compileTestJava {
                inputs.property("moduleName", moduleName)
                def mods = subproj.ext.has("testModules") ? subproj.ext.get("testModules") : []
                mods.add('org.junit.jupiter.api')
                doFirst {
                    options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', mods.join(','),
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                    ]
                    mods.forEach { mod -> options.compilerArgs += [ '--add-reads', "$moduleName=$mod"] }
                    classpath = files()
                }
            }
            test {
                inputs.property("moduleName", moduleName)
                def mods = subproj.ext.has("testModules") ? subproj.ext.get("testModules") : []
                mods.add('org.junit.jupiter.api')
                doFirst {
                    jvmArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'ALL-MODULE-PATH',
                        '--add-opens', "$moduleName/$moduleName=org.junit.platform.commons",
                        '--add-opens', "$moduleName/$moduleName=org.mockito",
                        '--patch-module', "$moduleName=" + files(sourceSets.main.output.resourcesDir, sourceSets.test.output).asPath,
                    ]
                    mods.forEach { mod -> jvmArgs += [ '--add-reads', "$moduleName=$mod"] }
                    classpath = files()
                }
            }

            javadoc {
                options.addStringOption('-module-path', classpath.asPath)
            }
        }
    }

    sourceSets {
        main {
            java {
                if (!jpms) {
                    exclude '**/module-info.java'
                }
            }
        }
    }

    jar {
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
    }

    test {
        useJUnitPlatform()
    }

    pmd {
        ignoreFailures = true
        consoleOutput = true
        incrementalAnalysis = true
        toolVersion = pmdVersion
        sourceSets = [sourceSets.main]
        rulePriority = 4
        ruleSets = []
        ruleSetConfig = resources.text.fromFile(rootProject.file('buildtools/src/main/resources/pmd/pmd.xml'))
    }

    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = checkstyleVersion
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from javadoc
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from sourceSets.main.allSource
    }

    task processConfig(type: Copy) {
        from('src/main/cfg') {
            include '**/*.cfg'
        }
        into 'build/cfg/main'
    }

    classes {
        classes.dependsOn processConfig
    }

    artifacts {
        archives javadocJar
        archives sourceJar
    }

    spotbugs {
        ignoreFailures = true
        excludeFilter = rootProject.file('buildtools/src/main/resources/spotbugs/exclusions.xml')
        toolVersion = spotbugsVersion
        // Java modules doesn't work with google's findbugs dependency
        if (JavaVersion.current().isJava11Compatible() && project.hasProperty("jpms")) {
            sourceSets = []
        } else {
            sourceSets = [sourceSets.main]
        }
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    license {
        include "**/*.java"
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                pom {
                    packaging = 'jar'
                    url = 'https://www.trellisldp.org'
                    inceptionYear = '2017'
                    name = 'Trellis Linked Data Server'
                    description = 'The core components for a Trellis linked data server'

                    organization {
                        name = project.vendor
                        url = project.homepage
                    }

                    developers {
                        developer {
                            id = 'acoburn'
                            name = 'Aaron Coburn'
                            email = 'acoburn (at) apache (dot) org'
                        }
                    }

                    scm {
                        url = 'https://github.com/trellis-ldp/trellis'
                        connection = 'scm:git:https://github.com/trellis-ldp/trellis.git'
                        developerConnection = 'scm:git:git@github.com:trellis-ldp/trellis.git'
                        tag = 'HEAD'
                    }

                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                            comments = 'Copyright (c) 2017-2019 Trellis LDP'
                        }
                    }

                }
                pom.withXml {
                    // eliminate test-scoped dependencies
                    asNode().dependencies.removeAll { dep -> dep.scope == "test" }
                }

                from components.java

                artifact(sourceJar) {
                    classifier = 'sources'
                }

                artifact(javadocJar) {
                    classifier = 'javadoc'
                }
            }
        }
        repositories {
            maven {
                def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv("SONATYPE_USERNAME") ?: ""
                def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv("SONATYPE_PASSWORD") ?: ""
                if (version.endsWith("SNAPSHOT")) {
                    url "https://oss.sonatype.org/content/repositories/snapshots/"
                } else {
                    url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                }
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    tasks.withType(PublishToMavenRepository) {
        onlyIf {
            ! omitFromMavenPublishing.contains(subproj.name)
        }
    }
    tasks.withType(PublishToMavenLocal) {
        onlyIf {
            ! omitFromMavenPublishing.contains(subproj.name)
        }
    }
    task install(dependsOn: [assemble, publishToMavenLocal])
    task upload(dependsOn: [assemble, publish])

    signing {
        required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications
    }

    processResources {
        outputs.upToDateWhen { false }
        filesMatching(['**/features.xml', '**/banner.txt']) {
            expand project.properties
        }
    }

    task docs(type: Javadoc) {
        outputs.upToDateWhen { false }
        source sourceSets.main.allJava
        classpath = files(sourceSets.main.compileClasspath)
        destinationDir = new File(projectDir, "docs/${version}")
        options {
            tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
            links "https://docs.oracle.com/javase/8/docs/api/"
            links 'https://docs.oracle.com/javaee/7/api/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://commons.apache.org/proper/commons-codec/apidocs/'
            links 'https://activemq.apache.org/maven/apidocs/'
            links 'https://jena.apache.org/documentation/javadoc/rdfconnection/'
            links 'https://kafka.apache.org/20/javadoc/'
            links 'https://rabbitmq.github.io/rabbitmq-java-client/api/current/'
            links 'https://www.dropwizard.io/1.3.5/dropwizard-core/apidocs/'
        }

        if (JavaVersion.current().isJava11Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    javadoc {
        options.tags = ["apiNote:a:API Note:",
                "implSpec:a:Implementation Requirements:",
                "implNote:a:Implementation Note:"]

        if (JavaVersion.current().isJava11Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    sonarqube {
        skipProject = JavaVersion.current().isJava10Compatible()
    }

    afterReleaseBuild.dependsOn docs
    afterReleaseBuild.dependsOn publish
    afterReleaseBuild.dependsOn assemble

    afterEvaluate {
        if (! omitFromJacocoReporting.contains(subproj.name)) {
            jacoco {
                applyTo subproj.tasks.matching { it.name == 'junitPlatformTest' }
            }
        }
    }
}

configure(rootProject) {

    task apidocs(type: Javadoc, dependsOn: getTasksByName('docs', true)) {
        failOnError false
        outputs.upToDateWhen { false }
        destinationDir = new File(projectDir, "docs/apidocs/${project.version}")
        title = "Trellis Linked Data Server Documentation"
        exclude '**/impl/*'
        exclude '**/*Tests.java'
        options {
            tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
            memberLevel = JavadocMemberLevel.PUBLIC
            links "https://docs.oracle.com/javase/8/docs/api/"
            links 'https://docs.oracle.com/javaee/7/api/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://commons.apache.org/proper/commons-codec/apidocs/'
            links 'https://activemq.apache.org/maven/apidocs/'
            links 'https://jena.apache.org/documentation/javadoc/rdfconnection/'
            links 'https://kafka.apache.org/20/javadoc/'
            links 'https://rabbitmq.github.io/rabbitmq-java-client/api/current/'
            links 'https://www.dropwizard.io/1.3.5/dropwizard-core/apidocs/'
        }

        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }

        source subprojects.collect { project -> project.sourceSets.main.allJava }
        classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
    }

    sonarqube {
        properties {
            property "sonar.projectName", "Trellis Linked Data Server"
            property "sonar.projectKey", "org.trellisldp:trellis"
            property "sonar.links.homepage", "https://www.trellisldp.org"
            property "sonar.links.issue", "https://github.com/trellis-ldp/trellis/issues"
            property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/trellis.git"
        }
    }

    // Ignore alpha, beta, milestone and release candidates
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject("Release Candidate")
                }
            }
        }
    }

    task jacocoMerge(type: JacocoMerge) {
        subprojects.findAll { ! omitFromJacocoReporting.contains(it.name) }
                .each { subproj ->
            executionData fileTree(dir: "${subproj.buildDir}/jacoco", include: '*.exec')
            dependsOn subproj.tasks.withType(Test)
        }
    }

    task jacocoRootReport(type: JacocoReport, dependsOn: jacocoMerge) {
        sourceDirectories.from(files(subprojects
                .findAll { ! omitFromJacocoReporting.contains(it.name) }
                .sourceSets.main.allSource.srcDirs))
        classDirectories.from(files(subprojects
                .findAll { ! omitFromJacocoReporting.contains(it.name) }
                .sourceSets.main.output))
        executionData jacocoMerge.destinationFile
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    coveralls {
        sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
        jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
    }

    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }

    tasks.coveralls {
        dependsOn 'jacocoRootReport'
    }

    task getVersion {
        doLast {
            println project.version
        }
    }
}
