plugins {
    id 'com.github.ben-manes.versions' version "0.38.0"
    id 'com.github.hierynomus.license' version "0.15.0"
    id 'net.researchgate.release' version "2.8.1"
    id 'org.gradle.test-retry' version "1.2.1"
    id 'org.owasp.dependencycheck' version "6.1.6"
    id 'org.sonarqube' version "3.2.0"
}

ext {
    // Core dependencies
    commonsCodecVersion = "1.15"
    commonsIoVersion = "2.8.0"
    commonsLangVersion = "3.12.0"
    commonsRdfVersion = "0.5.0"
    slf4jVersion = "1.7.30"

    // JakartaEE
    activationApiVersion = "1.2.2"
    annotationApiVersion = "1.3.5"
    cdiApiVersion = "2.0.2"
    injectApiVersion = "1.0.1"
    jaxbApiVersion = "2.3.3"
    jaxrsApiVersion = "2.1.6"
    jsonApiVersion = "1.1.6"
    jsonbApiVersion = "1.0.2"
    managementApiVersion = "1.1.4"
    validationApiVersion = "2.0.2"

    // MicroProfile
    microprofileVersion = "3.3"
    microprofileReactiveMessagingVersion = "1.0"

    // Component dependencies
    guavaVersion = "30.1.1-jre"
    jacksonVersion = "2.12.3"
    jdbiVersion = "3.20.0"
    jenaVersion = "4.0.0"
    jsonldVersion = "0.13.3"
    liquibaseVersion = "4.3.5"
    mustacheVersion = "0.9.7"
    quarkusVersion = "1.13.4.Final"
    rxjavaVersion = "2.2.21"
    snakeyamlVersion = "1.28"

    // Databases
    h2Version = "1.4.200"
    mariadbVersion = "2.7.1"
    postgresVersion = "42.2.20"

    // Testing
    awaitilityVersion = "4.1.0"
    bouncycastleVersion = "1.68"
    commonsTextVersion = "1.9"
    cxfVersion = "3.4.3"
    glassfishJaxbVersion = "2.3.3"
    jerseyVersion = "2.32"
    junitVersion = "5.7.1"
    hamcrestVersion = "2.2"
    logbackVersion = "1.2.3"
    mockitoVersion = "3.10.0"
    qpidVersion = "8.0.4"
    sleepycatVersion = "18.3.12"
    smallryeConfigVersion = "2.3.0"
    smallryeHealthVersion = "2.2.5"
    smallryeJwtVersion = "2.4.3"
    smallryeReactiveVersion = "1.0.8"
    smallryeReactiveOperatorsVersion = "1.0.13"
    testcontainersVersion = "1.15.3"
    weldVersion = "2.0.2.Final"
    yassonVersion = "1.0.8"

    // Containers
    postgresContainerVersion = "10.8"

    // Tooling
    jacocoToolVersion = "0.8.6"
    checkstyleToolVersion = "8.34"
    pitestToolVersion = "1.5.2"
    pitestJunitPluginVersion = "0.12"
    pmdToolVersion = "6.24.0"

    // OSGi
    elVersion = "3.0.3"
    failureaccessVersion = "1.0.1"
    interceptorVersion = "1.2.5"
    jenaVersionRange = "[4,5)"
    karafVersion = "4.3.1"
    microprofileConfigVersion = "1.4"
    microprofileHealthVersion = "2.2"
    microprofileMetricsVersion = "2.3.2"
    microprofileJwtVersion = "1.1.1"
    microprofileOpenapiVersion = "1.1.2"
    mustacheOsgiVersion = "0.9.6_1"
    osgiVersion = "8.0.0"
    osgiCompendiumVersion = "5.0.0"
    paxExamVersion = "4.13.4"
    spiflyVersion = "1.3.2"

    omitFromJacocoReporting = [
        'trellis-bom',
        'trellis-karaf',
        'trellis-openliberty',
        'trellis-osgi',
        'trellis-quarkus',
        'trellis-test'
    ]

    omitFromMavenPublishing = [
        'trellis-osgi',
        'trellis-quarkus',
        'trellis-openliberty'
    ]
}

allprojects { subproj ->

    apply plugin: 'checkstyle'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.gradle.test-retry'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'pmd'
    apply plugin: 'signing'

    ext {
        vendor = 'Trellis LDP'
        homepage = 'https://www.trellisldp.org/'
        docURL = 'https://www.trellisldp.org/docs/trellis/current/apidocs/'
        license = 'Apache-2.0'

    }

    jacoco.toolVersion = jacocoToolVersion

    group = 'org.trellisldp'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        testImplementation platform("org.junit:junit-bom:$junitVersion")

        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.apiguardian:apiguardian-api"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    }

    dependencyCheck {
        analyzers {
            assemblyEnabled = false
            retirejs {
                enabled = false
            }
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    release {
        tagTemplate = '$name-$version'
        git {
            /* Note: for maintenance branches, change this to the appropriate branch name. */
            requireBranch = 'main'
            signTag = true
        }
    }

    configurations.all {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'

        resolutionStrategy.dependencySubstitution {
            substitute module("org.apache.geronimo.specs:geronimo-annotation_1.2_spec") with module ("jakarta.annotation:jakarta.annotation-api:$annotationApiVersion")
            substitute module("org.apache.geronimo.specs:geronimo-j2ee-management_1.1_spec") with module("jakarta.management.j2ee:jakarta.management.j2ee-api:$managementApiVersion")
            substitute module("org.glassfish.hk2.external:jakarta.inject") with module("jakarta.inject:jakarta.inject-api:$injectApiVersion")
            substitute module("javax.enterprise:cdi-api") with module("jakarta.enterprise:jakarta.enterprise.cdi-api:$cdiApiVersion")
            substitute module("javax.annotation:javax.annotation-api") with module("jakarta.annotation:jakarta.annotation-api:$annotationApiVersion")
            substitute module("javax.inject:javax.inject") with module("jakarta.inject:jakarta.inject-api:$injectApiVersion")
        }
    }
}

subprojects { subproj ->

    sourceCompatibility = 11
    targetCompatibility = 11

    plugins.withType(JavaPlugin).configureEach {
        java {
            modularity.inferModulePath = true
        }
    }

    afterEvaluate {
        if (subproj.ext.has('moduleName')) {
            def moduleName = subproj.ext.get('moduleName')

            compileJava {
                inputs.property("moduleName", moduleName)
                doFirst {
                    options.compilerArgs = [
                        '--module-path', classpath.asPath,
                    ]
                    classpath = files()
                }
            }
            compileTestJava {
                inputs.property("moduleName", moduleName)
                def mods = subproj.ext.has("testModules") ? subproj.ext.get("testModules") : []
                mods.add('org.junit.jupiter.api')
                mods.add('org.junit.jupiter.params')
                doFirst {
                    options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', mods.join(','),
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                    ]
                    mods.forEach { mod -> options.compilerArgs += [ '--add-reads', "$moduleName=$mod"] }
                    classpath = files()
                }
            }
            test {
                inputs.property("moduleName", moduleName)
                def mods = subproj.ext.has("testModules") ? subproj.ext.get("testModules") : []
                mods.add('org.junit.jupiter.api')
                mods.add('org.junit.jupiter.params')
                doFirst {
                    jvmArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'ALL-MODULE-PATH',
                        '--add-opens', "$moduleName/$moduleName=org.junit.platform.commons",
                        '--add-opens', "$moduleName/$moduleName=org.mockito",
                        '--patch-module', "$moduleName=" + files(sourceSets.main.output.resourcesDir, sourceSets.test.output).asPath,
                    ]
                    mods.forEach { mod -> jvmArgs += [ '--add-reads', "$moduleName=$mod"] }
                    classpath = files()
                }
            }

            javadoc {
                failOnError = false
                inputs.property("moduleName", moduleName)
                doFirst {
                    options.setModulePath(classpath.collect { f -> file(f) })
                }
            }
        }
    }

    jar {
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat "full"
            showStackTraces false
        }
        retry {
            maxRetries = (project.findProperty("trellis.test.retry") ?: "3").toInteger()
            maxFailures = 20
        }
    }

    pmd {
        ignoreFailures = true
        consoleOutput = true
        incrementalAnalysis = true
        toolVersion = pmdToolVersion
        sourceSets = [sourceSets.main]
        rulePriority = 4
        ruleSets = []
        ruleSetConfig = resources.text.fromFile(rootProject.file('buildtools/src/main/resources/pmd/pmd.xml'))
    }

    // Enable PMD analysis on request (e.g. ./gradlew build -P pmdAnalysis)
    pmdMain.enabled = project.hasProperty("pmdAnalysis")

    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = checkstyleToolVersion
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from javadoc
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar
        archives sourceJar
    }

    license {
        include "**/*.java"
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    downloadLicenses {
        includeProjectDependencies = true
        dependencyConfiguration = 'compileClasspath'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                pom {
                    packaging = 'jar'
                    url = 'https://www.trellisldp.org'
                    inceptionYear = '2017'
                    name = 'Trellis Linked Data Server'
                    description = 'The core components for a Trellis linked data server'

                    organization {
                        name = project.vendor
                        url = project.homepage
                    }

                    developers {
                        developer {
                            id = 'acoburn'
                            name = 'Aaron Coburn'
                            email = 'acoburn (at) apache (dot) org'
                        }
                    }

                    scm {
                        url = 'https://github.com/trellis-ldp/trellis'
                        connection = 'scm:git:https://github.com/trellis-ldp/trellis.git'
                        developerConnection = 'scm:git:git@github.com:trellis-ldp/trellis.git'
                        tag = 'HEAD'
                    }

                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            comments = 'Copyright (c) 2017 - 2020 Aaron Coburn and individual contributors'
                            distribution = 'repo'
                        }
                    }

                }
                pom.withXml {
                    // eliminate test-scoped dependencies
                    asNode().dependencies.removeAll { dep -> dep.scope == "test" }
                    // use provided scope for JavaEE dependencies
                    asNode().dependencies.each {
                        deps -> deps.findAll { dep -> dep.groupId[0].text().startsWith("jakarta.") }
                                    .each { dep -> dep.scope[0].value = "provided" } }
                }

                from components.java

                artifact(sourceJar) {
                    classifier = 'sources'
                }

                artifact(javadocJar) {
                    classifier = 'javadoc'
                }
            }
        }
        repositories {
            maven {
                def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv("SONATYPE_USERNAME") ?: ""
                def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv("SONATYPE_PASSWORD") ?: ""
                if (version.endsWith("SNAPSHOT")) {
                    url "https://oss.sonatype.org/content/repositories/snapshots/"
                } else {
                    url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                }
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    tasks.withType(PublishToMavenRepository) {
        onlyIf {
            ! omitFromMavenPublishing.contains(subproj.name)
        }
    }
    tasks.withType(PublishToMavenLocal) {
        onlyIf {
            ! omitFromMavenPublishing.contains(subproj.name)
        }
    }
    task install(dependsOn: [assemble, publishToMavenLocal])
    task upload(dependsOn: [assemble, publish])

    signing {
        required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications
    }

    tasks.withType(Sign) {
        onlyIf { !version.endsWith("SNAPSHOT") }
    }

    processResources {
        outputs.upToDateWhen { false }
        filesMatching(['**/features.xml', '**/banner.txt']) {
            expand project.properties
        }
    }

    javadoc {
        failOnError = false
        options.addBooleanOption('html5', true)
        options {
            tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
            links "https://docs.oracle.com/en/java/javase/11/docs/api/"
            links 'https://jakarta.ee/specifications/platform/8/apidocs/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://commons.apache.org/proper/commons-codec/apidocs/'
            links 'https://jena.apache.org/documentation/javadoc/rdfconnection/'
        }
    }

    sonarqube {
        skipProject = ! JavaVersion.current().isJava11()
    }

    afterReleaseBuild.dependsOn publish
    afterReleaseBuild.dependsOn assemble

    afterEvaluate {
        if (! omitFromJacocoReporting.contains(subproj.name)) {
            jacoco {
                applyTo subproj.tasks.matching { it.name == 'junitPlatformTest' }
            }
        }
    }
}

configure(rootProject) {

    task apidocs(type: Javadoc) {
        failOnError false
        outputs.upToDateWhen { false }
        destinationDir = new File(projectDir, "docs/apidocs/${project.version}")
        title = "Trellis Linked Data Server Documentation"
        exclude '**/impl/*'
        exclude '**/*Tests.java'
        exclude '**/module-info.java'
        options {
            tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
            memberLevel = JavadocMemberLevel.PUBLIC
            links "https://docs.oracle.com/en/java/javase/11/docs/api/"
            links 'https://jakarta.ee/specifications/platform/8/apidocs/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://commons.apache.org/proper/commons-codec/apidocs/'
            links 'https://jena.apache.org/documentation/javadoc/rdfconnection/'
        }

        source subprojects.collect { project -> project.sourceSets.main.allJava }
        classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })

        options.addBooleanOption('html5', true)
    }

    sonarqube {
        properties {
            property "sonar.projectKey", "org.trellisldp:trellis"
            property "sonar.projectName", "Trellis Linked Data Server"
            property "sonar.links.homepage", "https://www.trellisldp.org"
            property "sonar.links.issue", "https://github.com/trellis-ldp/trellis/issues"
            property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/trellis.git"
            property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
        }
    }

    // Ignore alpha, beta, milestone and release candidates
    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'b', 'cr', 'rc', 'm', 'mr'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject("Release Candidate")
                }
            }
        }
    }

    task jacocoMerge(type: JacocoMerge) {
        subprojects.findAll { ! omitFromJacocoReporting.contains(it.name) }
                .each { subproj ->
            executionData fileTree(dir: "${subproj.buildDir}/jacoco", include: '*.exec')
            dependsOn subproj.tasks.withType(Test)
        }
    }

    task jacocoRootReport(type: JacocoReport, dependsOn: jacocoMerge) {
        sourceDirectories.from(files(subprojects
                .findAll { ! omitFromJacocoReporting.contains(it.name) }
                .sourceSets.main.allSource.srcDirs))
        classDirectories.from(files(subprojects
                .findAll { ! omitFromJacocoReporting.contains(it.name) }
                .sourceSets.main.output))
        executionData jacocoMerge.destinationFile
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }

    task getVersion {
        doLast {
            println project.version
        }
    }
}
