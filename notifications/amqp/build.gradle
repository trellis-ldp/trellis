apply plugin: 'java-library'
apply plugin: 'osgi'

description = 'Trellis Notifications (AMQP)'

ext {
    moduleName = 'org.trellisldp.amqp'
}

dependencies {
    api("com.rabbitmq:amqp-client:$rabbitMqVersion")
    api("org.glassfish.hk2.external:javax.inject:$javaxInjectVersion")
    api project(':trellis-api')

    implementation("org.apache.tamaya:tamaya-api:$tamayaVersion")
    implementation("org.slf4j:slf4j-api:$slf4jVersion")

    testImplementation("ch.qos.logback:logback-classic:$logbackVersion")
    testImplementation("com.sleepycat:je:$sleepycatVersion")
    testImplementation("org.apache.commons:commons-rdf-simple:$commonsRdfVersion")
    testImplementation("org.apache.qpid:qpid-broker:$qpidVersion")
    testImplementation("org.apache.tamaya:tamaya-core:$tamayaVersion")
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
    testImplementation project(':trellis-event-serialization')
    testImplementation project(':trellis-vocabulary')
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', moduleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "${moduleName};version=${projectOsgiVersion}"
        instruction 'Require-Capability', '''\
            osgi.extender; filter:="(osgi.extender=osgi.serviceloader.processor)",
            osgi.serviceloader;
                filter:="(osgi.serviceloader=org.trellisldp.api.ActivityStreamService)";
                resolution:=mandatory; cardinality:=mandatory
            '''
    }
}

test {
    def port = Integer.toString(new ServerSocket(0).getLocalPort())
    systemProperty 'qpid.amqp.port', port
    systemProperty 'derby.stream.error.file', "$buildDir/derby.log"
    systemProperty 'trellis.amqp.uri', "amqp://guest:guest@localhost:${port}/testing"
}

