apply plugin: 'java-library'
apply plugin: 'biz.aQute.bnd.builder'

description = 'Trellis Notifications (JMS)'

configurations {
    activemq
}

ext {
    moduleName = 'org.trellisldp.jms'
    testModules = ['smallrye.config', 'org.trellisldp.vocabulary', 'org.trellisldp.event', 'org.apache.commons.rdf.simple']
}

dependencies {
    api("javax.jms:javax.jms-api:$jmsApiVersion")
    api("org.glassfish.hk2.external:javax.inject:$javaxInjectVersion")
    api project(':trellis-api')

    implementation("org.apache.activemq:activemq-client:$activeMqVersion")
    implementation("org.eclipse.microprofile.config:microprofile-config-api:$microprofileConfigVersion")
    implementation("org.slf4j:slf4j-api:$slf4jVersion")

    testImplementation("ch.qos.logback:logback-classic:$logbackVersion")
    testImplementation("io.smallrye:smallrye-config:$smallryeVersion")
    testImplementation("org.apache.commons:commons-rdf-simple:$commonsRdfVersion")
    testImplementation("org.mockito:mockito-core:$mockitoVersion")
    testImplementation project(':trellis-event-serialization')
    testImplementation project(':trellis-vocabulary')

    activemq("org.apache.activemq:activemq-broker:$activeMqVersion") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "org.apache.activemq", module: "activemq-client"
    }

    if (!project.sourceCompatibility.isJava11Compatible()) {
        testImplementation("org.apache.activemq:activemq-broker:$activeMqVersion")
    }
}

if (project.sourceCompatibility.isJava11Compatible()) {
    compileTestJava {
        doFirst {
            options.compilerArgs += [
                '--patch-module', "activemq.client=" + files(configurations.activemq).asPath,
            ]
        }
    }

    test {
        inputs.property("moduleName", moduleName)
        doFirst {
            jvmArgs += [
                '--patch-module', "activemq.client=" + files(configurations.activemq).asPath,
            ]
        }
    }
}
