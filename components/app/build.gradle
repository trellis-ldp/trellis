plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder'
    id 'info.solidsoft.pitest'
}

description = 'Trellis Application'

ext {
    moduleName = 'org.trellisldp.app'
    testModules = ['org.trellisldp.file', 'org.trellisldp.constraint', 'org.trellisldp.audit',
                   'org.trellisldp.io', 'org.trellisldp.rdfa', 'smallrye.config']
}

dependencies {
    api "jakarta.enterprise:jakarta.enterprise.cdi-api:$cdiApiVersion"
    api project(':trellis-api')
    api project(':trellis-http')
    api project(':trellis-vocabulary')

    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    testRuntimeOnly "jakarta.activation:jakarta.activation-api:$activationApiVersion"

    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
    testImplementation "io.smallrye.config:smallrye-config:$smallryeConfigVersion"
    testImplementation("org.jboss.weld:weld-junit5:$weldVersion") {
        exclude group: "org.jboss.spec.javax.interceptor", module: "jboss-interceptors-api_1.2_spec"
        exclude group: "org.jboss.spec.javax.el", module: "jboss-el-api_3.0_spec"
    }

    testImplementation project(':trellis-triplestore')
    testImplementation project(':trellis-constraint-rules')
    testImplementation project(':trellis-io-jena')
    testImplementation project(':trellis-file')
    testImplementation project(':trellis-rdfa')
    testImplementation project(':trellis-audit')
}

pitest {
    testPlugin = 'junit5'
    pitestVersion = pitestToolVersion
    threads = 4
}

if (project.sourceCompatibility.isJava11Compatible()) {
    test {
        inputs.property("moduleName", moduleName)
        doFirst {
            jvmArgs += [
                '--add-opens', "org.trellisldp.api/org.trellisldp.api=weld.core.impl",
                '--add-opens', "org.trellisldp.http/org.trellisldp.http.core=weld.core.impl",
                '--add-opens', "org.trellisldp.constraint/org.trellisldp.constraint=weld.core.impl",
                '--add-opens', "org.trellisldp.file/org.trellisldp.file=weld.core.impl",
                '--add-opens', "org.trellisldp.io/org.trellisldp.io=weld.core.impl"
            ]
        }
    }
}
