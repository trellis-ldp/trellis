plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder'
    id 'info.solidsoft.pitest'
}

description = 'Trellis Application'

ext {
    moduleName = 'org.trellisldp.app'
    testModules = ['org.trellisldp.file', 'org.trellisldp.constraint', 'org.trellisldp.audit',
                   'org.trellisldp.jena', 'org.trellisldp.rdfa', 'smallrye.config']
}

dependencies {
    api "jakarta.enterprise:jakarta.enterprise.cdi-api:$cdiApiVersion"
    api project(':trellis-api')
    api project(':trellis-common')
    api project(':trellis-vocabulary')

    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    testRuntimeOnly "jakarta.activation:jakarta.activation-api:$activationApiVersion"

    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
    testImplementation "io.smallrye.config:smallrye-config:$smallryeConfigVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation("org.jboss.weld:weld-junit5:$weldVersion") {
        exclude group: "org.jboss.spec.javax.interceptor", module: "jboss-interceptors-api_1.2_spec"
        exclude group: "org.jboss.spec.javax.el", module: "jboss-el-api_3.0_spec"
    }

    testImplementation project(':trellis-audit')
    testImplementation project(':trellis-constraint')
    testImplementation project(':trellis-file')
    testImplementation project(':trellis-jena')
    testImplementation project(':trellis-rdfa')
    testImplementation project(':trellis-triplestore')
}

pitest {
    pitestVersion = pitestToolVersion
    junit5PluginVersion = pitestJunitPluginVersion
    threads = 4
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs += [
            '--add-opens', "org.trellisldp.api/org.trellisldp.api=weld.core.impl",
            '--add-opens', "org.trellisldp.common/org.trellisldp.common=weld.core.impl",
            '--add-opens', "org.trellisldp.constraint/org.trellisldp.constraint=weld.core.impl",
            '--add-opens', "org.trellisldp.file/org.trellisldp.file=weld.core.impl",
            '--add-opens', "org.trellisldp.jena/org.trellisldp.jena=weld.core.impl",
            '--add-opens', "java.base/java.lang=weld.core.impl",
        ]
    }
}
