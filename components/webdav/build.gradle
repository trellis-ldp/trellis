plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder'
    id 'info.solidsoft.pitest'
}

description = 'Trellis WebDAV'

ext {
    moduleName = 'org.trellisldp.webdav'
    testModules = ['org.trellisldp.jena', 'org.trellisldp.http', 'org.trellisldp.audit', 'smallrye.config']
}

dependencies {
    api platform("org.eclipse.microprofile:microprofile:$microprofileVersion")

    api "jakarta.inject:jakarta.inject-api:$injectApiVersion"
    api "jakarta.enterprise:jakarta.enterprise.cdi-api:${cdiApiVersion}"
    api "jakarta.annotation:jakarta.annotation-api:$annotationApiVersion"
    api "jakarta.ws.rs:jakarta.ws.rs-api:$jaxrsApiVersion"
    api "org.apache.commons:commons-rdf-api:$commonsRdfVersion"
    api "org.eclipse.microprofile.metrics:microprofile-metrics-api"
    api project(':trellis-api')

    implementation "jakarta.xml.bind:jakarta.xml.bind-api:$jaxbApiVersion"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    implementation "org.apache.jena:jena-arq:$jenaVersion"
    implementation "org.eclipse.microprofile.config:microprofile-config-api"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation project(':trellis-common')
    implementation project(':trellis-vocabulary')

    testImplementation platform("org.glassfish.jersey:jersey-bom:$jerseyVersion")

    testImplementation "io.smallrye.config:smallrye-config:$smallryeConfigVersion"
    testImplementation "org.apache.commons:commons-rdf-simple:$commonsRdfVersion"
    testImplementation "org.glassfish.jersey.core:jersey-server"
    testImplementation "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2"
    testImplementation "org.glassfish.jersey.inject:jersey-hk2"
    testImplementation "org.glassfish.jersey.connectors:jersey-apache-connector"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation project(':trellis-http')
    testImplementation project(':trellis-audit')
    testImplementation project(':trellis-constraint')
    testImplementation project(':trellis-jena')
    testImplementation project(':trellis-notification-jackson')

    testRuntimeOnly "jakarta.activation:jakarta.activation-api:$activationApiVersion"
    testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
    testRuntimeOnly "org.glassfish.jaxb:jaxb-runtime:$glassfishJaxbVersion"
}

pitest {
    pitestVersion = pitestToolVersion
    junit5PluginVersion = pitestJunitPluginVersion
    threads = 4
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs += [
            '--add-opens', "java.base/java.net=jersey.client",
            '--add-opens', "$moduleName/org.trellisldp.webdav.impl=org.junit.platform.commons",
            '--add-opens', "$moduleName/org.trellisldp.webdav.impl=org.mockito",
            '--add-opens', "$moduleName/$moduleName=hk2.utils",
            '--add-opens', "org.trellisldp.http/org.trellisldp.http=hk2.utils",
        ]
    }
    mustRunAfter(':trellis-http:test')
}
