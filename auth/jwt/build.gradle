plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder'
    id 'info.solidsoft.pitest'
}

description = 'Trellis MicroProfile JWT-Auth'

ext {
    moduleName = 'org.trellisldp.jwt'
}

dependencies {
    api platform("org.eclipse.microprofile:microprofile:$microprofileVersion")

    api "jakarta.annotation:jakarta.annotation-api:$annotationApiVersion"
    api "jakarta.enterprise:jakarta.enterprise.cdi-api:$cdiApiVersion"
    api "jakarta.inject:jakarta.inject-api:$injectApiVersion"
    api "jakarta.ws.rs:jakarta.ws.rs-api:$jaxrsApiVersion"
    api "org.eclipse.microprofile.jwt:microprofile-jwt-auth-api"

    implementation "jakarta.xml.bind:jakarta.xml.bind-api:$jaxbApiVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "org.eclipse.microprofile.config:microprofile-config-api"
    implementation project(":trellis-common")

    testImplementation platform("org.glassfish.jersey:jersey-bom:$jerseyVersion")

    testImplementation "io.smallrye.config:smallrye-config:$smallryeConfigVersion"
    testImplementation "io.smallrye:smallrye-jwt:$smallryeJwtVersion"
    testImplementation "jakarta.json.bind:jakarta.json.bind-api:$jsonbApiVersion"
    testImplementation "org.eclipse:yasson:$yassonVersion"
    testImplementation "org.glassfish.jersey.core:jersey-server"
    testImplementation("org.jboss.weld:weld-junit5:$weldVersion") {
        exclude group: "org.jboss.spec.javax.interceptor", module: "jboss-interceptors-api_1.2_spec"
        exclude group: "org.jboss.spec.javax.el", module: "jboss-el-api_3.0_spec"
    }
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    testRuntimeOnly "jakarta.activation:jakarta.activation-api:$activationApiVersion"
    testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
}

pitest {
    pitestVersion = pitestToolVersion
    junit5PluginVersion = pitestJunitPluginVersion
    threads = 4
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs += [
            '--add-opens', "$moduleName/$moduleName=weld.junit5",
            '--add-opens', "$moduleName/$moduleName=weld.core.impl",
            '--add-opens', 'java.base/java.lang=weld.core.impl',
        ]
    }
}
